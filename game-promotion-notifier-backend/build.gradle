plugins {
	id 'org.springframework.boot' version '2.5.0' apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'

    group = 'io.mkrzywanski'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '16'

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            showStackTraces = true
            showStandardStreams = true
        }

    }

	dependencies {
		implementation "org.codehaus.groovy:groovy:3.0.8"
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

        testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0")
		testImplementation "org.spockframework:spock-core"
        testImplementation 'org.exparity:hamcrest-date:2.0.7'
		testImplementation "org.hamcrest:hamcrest-core:2.2"
        testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
        testRuntimeOnly "net.bytebuddy:byte-buddy:1.11.0"

        implementation 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'

        testImplementation 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
