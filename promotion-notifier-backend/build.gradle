buildscript {
    ext {
        springBootVersion = "2.6.2"
        groovyVersion = '3.0.9'
        springBootVersion = "2.6.2"
        springDepenedcyMgmtVersion = '1.0.11.RELEASE'
        springCloudVersion = '3.1.0'
        testcontainersVersion = '1.16.2'
        awaitilityVersion = '4.1.1'
        wiremockVersion = '2.27.2'
        baseDockerImage = 'openjdk:17.0.1-slim'
        getStubsJarFromProject = this.&getStubsJarFromProject
    }
}

plugins {
	id 'org.springframework.boot' version "${springBootVersion}" apply false
	id 'io.spring.dependency-management' version "${springDepenedcyMgmtVersion}" apply false
	id 'org.springframework.cloud.contract' version "${springCloudVersion}" apply false
    id 'com.google.cloud.tools.jib' version '3.1.4' apply false
    id "io.freefair.lombok" version "6.3.0"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    group = 'io.mkrzywanski'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'


    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            showStackTraces = true
            showStandardStreams = true
        }
    }

    tasks.withType(Checkstyle) {
        exclude '**/generated-test-sources/**'
    }

	dependencies {
//		implementation "org.codehaus.groovy:groovy:3.0.9"
        implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'

        testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0")
		testImplementation "org.spockframework:spock-core"
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.8.1'

        testImplementation 'org.exparity:hamcrest-date:2.0.7'
		testImplementation "org.hamcrest:hamcrest-core:2.2"
        // https://mvnrepository.com/artifact/org.assertj/assertj-core
        testImplementation 'org.assertj:assertj-core:3.21.0'
        testRuntimeOnly "net.bytebuddy:byte-buddy:1.11.0"

        implementation 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'

        testImplementation 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

def getStubsJarFromProject(String projectName) {
    project(projectName).getTasksByName('verifierStubsJar', false).first().outputs.files
}
