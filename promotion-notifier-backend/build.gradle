buildscript {
    ext {
        keycloakVersion = '18.0.0'
        lombokVersion = '1.18.28'
        springVersion = '6.1.11'
        groovyVersion = '4.0.0'
        springBootVersion = '3.1.3'
        springDepenedcyMgmtVersion = '1.1.3'
        springCloudVersion = '4.0.4'
        testcontainersVersion = '1.19.0'
        awaitilityVersion = '4.2.0'
        wiremockVersion = '3.0.1'
        baseDockerImage = 'openjdk:17.0.1-slim'
        hamcrestVersion = '2.2'
        getStubsJarFromProject = this.&getStubsJarFromProject
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDepenedcyMgmtVersion}" apply false
    id 'org.springframework.cloud.contract' version "${springCloudVersion}" apply false
    id 'com.google.cloud.tools.jib' version '3.3.1' apply false
    id "io.freefair.lombok" version "8.3"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    group = 'io.mkrzywanski'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'


    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            showStackTraces = true
            showStandardStreams = true
            info {
                events "started", "skipped", "failed"
                exceptionFormat "full"
            }
        }
    }

    tasks.withType(Checkstyle) {
        exclude '**/generated-test-sources/**'
    }

    dependencies {
        implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.0'

        testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
        testImplementation "org.spockframework:spock-core"
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.1'

        testImplementation 'org.exparity:hamcrest-date:2.0.8'
        testImplementation "org.hamcrest:hamcrest-core:${hamcrestVersion}"
        testImplementation 'org.assertj:assertj-core:3.24.2'
        testRuntimeOnly "net.bytebuddy:byte-buddy:1.14.7"

        implementation "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    }
}

final def serviceProjects = ['pn-api-gateway', 'pn-email-service', 'pn-subscription-service', 'pn-matching-service', 'scrapper-app', 'subscription-app', 'pn-user-service'] as Set
final def loggingDir = rootProject.projectDir.path + '/logging'

subprojects.findAll{it.name in serviceProjects}
        .each {
            it.sourceSets.main.resources.srcDirs += loggingDir
        }

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

def getStubsJarFromProject(String projectName) {
    project(projectName).getTasksByName('verifierStubsJar', false).first().outputs.files
}
