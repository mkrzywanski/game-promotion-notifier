import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'com.bmuschko.docker-remote-api' version '7.2.0'
    id "nebula.java-cross-compile" version "6.2.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation("org.keycloak:keycloak-server-spi:${keycloakVersion}")
    implementation("org.keycloak:keycloak-server-spi-private:${keycloakVersion}")
    implementation("org.keycloak:keycloak-services:${keycloakVersion}")
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
    testImplementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.3'

    testImplementation project(':pn-shared-test-modules:keycloak-test-container')
    testImplementation "org.testcontainers:selenium:1.16.3"
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.1.2'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver:4.1.2'
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.32.0"
    testImplementation 'com.jayway.jsonpath:json-path-assert:2.6.0'
    testImplementation project(":pn-shared-test-modules:common")
}

task createDockerfile(type: Dockerfile) {
    dependsOn jar
    from "jboss/keycloak:${keycloakVersion}"
    copyFile("libs/${jar.archiveFile.get().asFile.name}", '/opt/jboss/keycloak/standalone/deployments/user-registered-listener.jar')
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    dockerFile.set(file("${buildDir.name}/docker/Dockerfile"))
    inputDir.set(file("${buildDir.name}"))
    images.add('keycloakk')
}