import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '9.3.2'
    id 'io.spring.dependency-management'
    id 'groovy'
}

sourceSets {
    integTest {
        groovy {
            srcDirs = ["${projectDir}/src/integTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/integTest/resources"]
        }
    }

    contractTest {
        groovy {
            srcDirs = ["${projectDir}/src/contractTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/contractTest/resources"]
        }
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly

    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation("org.keycloak:keycloak-server-spi:${keycloakVersion}")
    implementation("org.keycloak:keycloak-server-spi-private:${keycloakVersion}")
    implementation("org.keycloak:keycloak-services:${keycloakVersion}")

    testImplementation project(':pn-shared-test-modules:keycloak-test-container')
    testImplementation "com.github.dasniko:testcontainers-keycloak:${keycloakTestcontainersVersion}"
    testImplementation "org.testcontainers:selenium:${testcontainersVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:${wiremockVersion}"
    testImplementation 'com.jayway.jsonpath:json-path-assert:2.7.0'
    testImplementation project(":pn-shared-test-modules:common")
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'

    contractTestImplementation project(":pn-shared-test-modules:common")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.withType(Test).all {
    jvmArgs += '--add-opens=java.base/java.util=ALL-UNNAMED'
}

tasks.register('contractTest', Test) {
    useJUnitPlatform()
    description = 'Runs contract tests.'
    group = 'verification'

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    testLogging {

        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.findByName('test').dependsOn('contractTest')

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    testLogging {
        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.findByName('integrationTest').dependsOn('jar')

idea {
    module {
        sourceDirs -= project.sourceSets.integTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integTest.groovy.srcDirs
    }
}


task createDockerfile(type: Dockerfile, group: 'build') {
    dependsOn jar
    from "jboss/keycloak:${keycloakVersion}"
    copyFile("libs/${jar.archiveFile.get().asFile.name}", '/opt/jboss/keycloak/standalone/deployments/user-registered-listener.jar')
}

task buildImage(type: DockerBuildImage, group: 'build') {
    dependsOn createDockerfile
    dockerFile.set(file("${buildDir.name}/docker/Dockerfile"))
    inputDir.set(file("${buildDir.name}"))
    images.add('keycloakk')
}