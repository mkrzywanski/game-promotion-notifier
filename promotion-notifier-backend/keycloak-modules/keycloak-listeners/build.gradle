import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'com.bmuschko.docker-remote-api' version '7.3.0'
    id "nebula.java-cross-compile" version "6.2.0"
    id 'io.spring.dependency-management'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    integTest {
        groovy {
            srcDirs = ["${projectDir}/src/integTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/integTest/resources"]
        }
    }

    contractTest {
        groovy {
            srcDirs = ["${projectDir}/src/contractTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/contractTest/resources"]
        }
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly

    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation("org.keycloak:keycloak-server-spi:${keycloakVersion}")
    implementation("org.keycloak:keycloak-server-spi-private:${keycloakVersion}")
    implementation("org.keycloak:keycloak-services:${keycloakVersion}")
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
    testImplementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.3'

    testImplementation project(':pn-shared-test-modules:keycloak-test-container')
    testImplementation "org.testcontainers:selenium:1.17.0"
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.1.3'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver:4.1.3'
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.33.1"
    testImplementation 'com.jayway.jsonpath:json-path-assert:2.7.0'
    testImplementation project(":pn-shared-test-modules:common")
    testImplementation group: 'org.spockframework', name: 'spock-spring'
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'

    contractTestImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:${springCloudVersion}"
    contractTestImplementation project(":pn-shared-test-modules:common")
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.32.0")
    contractTestRuntimeClasspath getStubsJarFromProject(':pn-user-service:user-app')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudVersion}"
    }
}

tasks.register('contractTest', Test) {
    useJUnitPlatform()
    description = 'Runs contract tests.'
    group = 'verification'

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    testLogging {

        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.findByName('test').dependsOn('contractTest')

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    testLogging {
        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        sourceDirs -= project.sourceSets.integTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integTest.groovy.srcDirs
    }
}


task createDockerfile(type: Dockerfile, group: 'build') {
    dependsOn jar
    from "jboss/keycloak:${keycloakVersion}"
    copyFile("libs/${jar.archiveFile.get().asFile.name}", '/opt/jboss/keycloak/standalone/deployments/user-registered-listener.jar')
}

task buildImage(type: DockerBuildImage, group: 'build') {
    dependsOn createDockerfile
    dockerFile.set(file("${buildDir.name}/docker/Dockerfile"))
    inputDir.set(file("${buildDir.name}"))
    images.add('keycloakk')
}