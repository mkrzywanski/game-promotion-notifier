plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.google.cloud.tools.jib'
}

sourceSets {
    contractTest {
        groovy {
            srcDirs = ["${projectDir}/src/contractTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/contractTest/resources"]
        }
    }
}

configurations {
    contractTestImplementation{
        extendsFrom testImplementation
    }
    contractTestRuntimeOnly {
        extendsFrom(testRuntimeOnly)
    }
}


dependencies {
    implementation "org.springframework.cloud:spring-cloud-starter-gateway"
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    testImplementation "org.wiremock:wiremock-standalone:${wiremockVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.spockframework:spock-spring'
    testImplementation "org.springframework.cloud:spring-cloud-test-support"

    contractTestImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner"
    contractTestImplementation project(":pn-shared-test-modules:common")
    contractTestImplementation("org.wiremock:wiremock-standalone${wiremockVersion}")
    contractTestRuntimeOnly getStubsJarFromProject(':pn-subscription-service:subscription-app')
    
}

jib {
    from {
        image = "${baseDockerImage}"
    }
    to {
        image = "${project.name}/${project.version}"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.register('contractTest', Test) {
    useJUnitPlatform()
    description = 'Runs contract tests.'
    group = 'verification'

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    testLogging {

        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.findByName('test').dependsOn('contractTest')