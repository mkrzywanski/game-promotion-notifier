plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
}

sourceSets {
    integTest {
        groovy {
            srcDirs = ["${projectDir}/src/integTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/integTest/resources"]
        }
    }
    contractTest {
        groovy {
            srcDirs = ["${projectDir}/src/contractTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/contractTest/resources"]
        }
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly

    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation project(':pn-webservice-common')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    integTestImplementation 'org.keycloak:keycloak-admin-client:15.0.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    //newer wiremock than Spring Cloud Contract to resolve opentest4j runtime issues
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.32.0")

    testImplementation group: 'org.spockframework', name: 'spock-spring'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: "${awaitilityVersion}"
    testImplementation "org.testcontainers:spock:${testcontainersVersion}"
    testImplementation "org.testcontainers:rabbitmq:${testcontainersVersion}"

    // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-contract-wiremock
    integTestImplementation "org.springframework.cloud:spring-cloud-contract-wiremock:${springCloudVersion}"

    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.30.1")
    contractTestRuntimeClasspath getStubsJarFromProject(':pn-site-scrapper:scrapper-app')
    contractTestRuntimeClasspath getStubsJarFromProject(':pn-user-service:user-app')
    contractTestRuntimeClasspath getStubsJarFromProject(':pn-subscription-service:subscription-app')
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    testLogging {

        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.register('contractTest', Test) {
    useJUnitPlatform()
    description = 'Runs contract tests.'
    group = 'verification'

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    testLogging {

        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

tasks.findByName('integrationTest').dependsOn('contractTest')

idea {
    module {
        sourceDirs -= project.sourceSets.integTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integTest.groovy.srcDirs
    }
}