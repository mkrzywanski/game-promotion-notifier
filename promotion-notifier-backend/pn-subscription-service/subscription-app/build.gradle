import org.springframework.cloud.contract.verifier.config.TestFramework

plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.springframework.cloud.contract'
    id 'com.google.cloud.tools.jib'
}

contractTest {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            boolean skipTests = Boolean.parseBoolean(project.findProperty('SKIP_TESTS') ?: "false")
            if (result.testCount == 0 && !skipTests) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

sourceSets {
    integTest {
        groovy {
            srcDirs = ["${projectDir}/src/integTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/integTest/resources"]
        }
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation project(":pn-subscription-service:subscription-domain")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-elasticsearch'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "io.micrometer:micrometer-registry-prometheus"

    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.33.2")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'org.spockframework', name: 'spock-spring'
    testImplementation 'org.springframework.security:spring-security-test'

    integTestImplementation "org.testcontainers:elasticsearch:${testcontainersVersion}"
    integTestImplementation 'org.springframework:spring-webflux'
    integTestImplementation "org.keycloak:keycloak-admin-client:${keycloakVersion}"
    integTestImplementation project(":pn-shared-test-modules:embedded-keycloak-spring-configuration")

    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    contractTestImplementation("com.jayway.jsonpath:json-path:2.7.0")
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.33.2")
    contractTestImplementation 'com.c4-soft.springaddons:spring-security-oauth2-test-addons:4.3.0'
    contractTestImplementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"

}

contracts {
    testFramework = TestFramework.SPOCK
    testMode = 'MockMvc'
    packageWithBaseClasses = 'io.mkrzywanski.pn.subscription.app.contracts'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudVersion}"
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    testLogging {
        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        sourceDirs -= project.sourceSets.integTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integTest.groovy.srcDirs
    }
}

jib {
    from {
        image = "${baseDockerImage}"
    }
}