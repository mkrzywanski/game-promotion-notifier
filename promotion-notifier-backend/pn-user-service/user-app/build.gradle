import org.springframework.cloud.contract.verifier.config.TestFramework

plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.springframework.cloud.contract'
}

contracts {
    testFramework = TestFramework.SPOCK
    testMode = 'MockMvc'
    packageWithBaseClasses = 'io.mkrzywanski.pn.user.app.contract'
}

contractTest {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            boolean skipTests = Boolean.parseBoolean(project.findProperty('SKIP_TESTS') ?: "false")
            if (result.testCount == 0 && !skipTests) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

sourceSets {
    integTest {
        groovy {
            srcDirs = ["${projectDir}/src/integTest/groovy"]
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            resources.srcDirs = ["${projectDir}/src/integTest/resources"]
        }
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation project(':pn-webservice-common')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "io.micrometer:micrometer-registry-prometheus"
    // https://mvnrepository.com/artifact/io.opentelemetry.instrumentation/opentelemetry-spring-boot-autoconfigure
    implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-autoconfigure:1.11.0-alpha'
    // https://mvnrepository.com/artifact/io.opentelemetry/opentelemetry-api
//    implementation 'io.opentelemetry:opentelemetry-api:1.11.0'
    // https://mvnrepository.com/artifact/io.opentelemetry/opentelemetry-sdk
    implementation 'io.opentelemetry:opentelemetry-sdk:1.11.0'
    // https://mvnrepository.com/artifact/io.opentelemetry.instrumentation/opentelemetry-jaeger-exporter-starter
    implementation 'io.opentelemetry.instrumentation:opentelemetry-jaeger-exporter-starter:1.11.0-alpha'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'org.spockframework', name: 'spock-spring'
    integTestImplementation "org.testcontainers:postgresql:${testcontainersVersion}"

    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    //https://github.com/marcingrzejszczak/jsonassert/issues/57
    contractTestImplementation("com.jayway.jsonpath:json-path:2.7.0")
    contractTestImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.32.0")
}

checkstyleContractTest {
    enabled = false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudVersion}"
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    testLogging {
        events("passed", "skipped", "failed")
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        sourceDirs -= project.sourceSets.integTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integTest.groovy.srcDirs
    }
}
